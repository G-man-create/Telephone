plugins {
  id 'java'
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0'
}

group 'program'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(16)
  }
}

application {
  mainClass = 'program.telephone.Main'
  applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
}

javafx {
  version = '17.0.2'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  implementation 'org.openjfx:javafx-controls:17.0.2'
  implementation 'org.openjfx:javafx-fxml:17.0.2'
  implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
  implementation 'org.apache.logging.log4j:log4j-api:2.20.0'

  testImplementation platform('org.junit:junit-bom:5.9.1')
  testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
  useJUnitPlatform()
}

tasks.register('fatJar', Jar) {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
  archiveClassifier = 'all'
  from sourceSets.main.output
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
  }
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

sourceSets {
  main {
    resources {
      srcDirs = ['src/main/resources']
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
  source = sourceSets.main.allJava
  options.memberLevel = JavadocMemberLevel.PRIVATE
}